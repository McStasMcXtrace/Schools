/*******************************************************************************
*         McStas instrument definition URL=http://www.mcstas.org
*
* Instrument: test (rename also the example and DEFINE lines below)
*
* %Identification
* Written by: Mads Bertelsen (mads.bertelsen@esss.se)
* Date: 19/5 2022
* Origin: European Spallation Source, DMSC
* %INSTRUMENT_SITE: Templates
*
* Reference solution for exercise at McStas school
*
* %Description
* Exercise in using guide components in McStas
*
* Example: mcrun Exercise_guides.instr guide_length=10.0 guide_width=0.04
*
* %Parameters
* lambda_min: [AA]     Lower bound of simulated wavelength range
* lambda_max: [AA]     Upper bound of simulated wavelength range
* guide_length: [m]    Length of guide
* guide_width: [m]     Width of guide
*
* %Link
* A reference/HTML link for more information
*
* %End
*******************************************************************************/
DEFINE INSTRUMENT reference(lambda_min=3.0, lambda_max=8.0, guide_length=1.0, guide_width=0.03)


TRACE

COMPONENT Origin = Progress_bar()
  AT (0, 0, 0) ABSOLUTE

// Source using instrument parameters to control wavelength range
COMPONENT source = Source_simple(
  xwidth=0.1, yheight=0.1,
  dist=2.0, focus_xw=0.03, focus_yh=0.03,
  lambda0=0.5*(lambda_max + lambda_min),
  dlambda=0.5*(lambda_max - lambda_min))
AT (0, 0, 0) RELATIVE Origin

// guide starting 2 m after source
COMPONENT guide = Guide_gravity(
  w1=guide_width, h1=0.03,
  w2=guide_width, h2=0.03,
  l=guide_length,
  R0=0.99, m=3, Qc=0.0217, alpha=2.0, W=0.003,
  G=-9.82)
AT (0,0,2) RELATIVE source

COMPONENT guide_end = Arm()
AT (0,0,guide_length) RELATIVE guide

// monitors
// Position sensetive detector
COMPONENT PSD_2D = PSD_monitor(
  xwidth=0.05, nx=200,
  yheight=0.05, ny=200,
  filename="PSD_2D.dat", restore_neutron=1)
AT (0, 0, 0.5) RELATIVE guide_end

// Divergence monitor
COMPONENT DIV_2D = Divergence_monitor(
  xwidth=0.05, nh=200, maxdiv_h=3.0,
  yheight=0.05, nv=200, maxdiv_v=3.0,
  filename="DIV_2D.dat", restore_neutron=1)
AT (0, 0, 0) RELATIVE PREVIOUS

// Wavelength monitor
COMPONENT Wavelength_1D = L_monitor(
  xwidth=0.05, yheight=0.05,
  Lmin=lambda_min, Lmax=1.2*lambda_max, nL=100,
  filename="Wavelength_1D.dat", restore_neutron=1)
AT (0, 0, 0) RELATIVE PREVIOUS

// Acceptance diagram
COMPONENT POS_DIV_2D = DivPos_monitor(
  xwidth=0.05, yheight=0.05,nh=200,
  ndiv=200, maxdiv_h=3.0,
  filename="POS_DIV_2D.dat", restore_neutron=1)
AT (0, 0, 0) RELATIVE PREVIOUS

END

