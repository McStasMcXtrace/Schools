/*******************************************************************************
*         McXtrace instrument definition URL=http://www.mcxtrace.org
*
* Instrument: Test_SX
*
* %Identification
* Written by: E. Farhi
* Date: Sept 26th 2019
* Origin: Synchrotron Soleil
* Version: 0.2
* %INSTRUMENT_SITE: Tests
*
* Unit-test instrument for the Single_crystal sample component.
*
* Simply a model source illuminating a SX sample.
* The sample itself is a Mo bulk crystal.
*
* %Parameters
* TTH: [deg.] Two theta rotation. Only displaces the last detector.
* sample: [str] sample |F2| reflection list (HKL)
* E0: [keV] incident photon energy
* rotX: [deg] sample rotation along X (left) axis.
* rotY: [deg] sample rotation along Y (vertical) axis.
*
* %End
*******************************************************************************/

DEFINE INSTRUMENT Test_SX(TTH=13.4, E0=15, string sample="4mea.lau", rotX=0, rotY=0)

TRACE

COMPONENT progress = Progress_bar()
AT (0,0,0) ABSOLUTE

COMPONENT src = Source_flat(
    yheight = 1e-3, xwidth = 1e-3, dist = 10, focus_xw = 1e-3,
    focus_yh = 1e-3, E0 = E0, dE = E0/100)
  AT (0, 0, 0) ABSOLUTE

COMPONENT psd0 = PSD_monitor(
    nx = 100, ny = 100, filename = "psd0", xwidth = 2e-3, yheight = 2e-3)
  AT (0, 0, 1e-9) RELATIVE src
  
COMPONENT sample_pos = Arm()
  AT (0, 0, 10) RELATIVE PREVIOUS

COMPONENT sample = Single_crystal(reflections=sample,
  radius = .5e-4, yheight = 1e-3, p_transmit = 0, mosaic=1)
  AT (0, 0, 0) RELATIVE sample_pos
  ROTATED (rotX,rotY,0) RELATIVE PREVIOUS
EXTEND %{
  if(!SCATTERED) ABSORB;
%}

COMPONENT psd4pi = PSD_monitor_4PI(
    nx = 1800, ny = 1800, filename = "psd4pi", radius = 0.5, restore_xray = 1)
  AT (0, 0, 0) RELATIVE sample_pos

COMPONENT detector = PSD_monitor(
    nx=2000, ny=2000, xwidth=0.1, yheight=0.1, filename="psd1",restore_xray=1)
  AT(0,0,0.1) RELATIVE sample_pos

COMPONENT ttharm= Arm()
  AT(0,0,0) RELATIVE sample_pos
  ROTATED (TTH,TTH,0) RELATIVE sample_pos

COMPONENT detector2 = PSD_monitor(
    nx=200, ny=200, xwidth=0.01, yheight=0.01, filename="psd2",restore_xray=1)
  AT(0,0,0.1) RELATIVE ttharm
  
END
